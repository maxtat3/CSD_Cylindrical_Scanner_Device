group 'edu.sintez'
version '1.0'

apply plugin: 'java'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

ext.jarName = "CSD"
ext.versionMajor = 1
ext.versionMinor = 2
/**
 * Is app release or release-candidate.
 *
 * true - nothing is added at the end of jar file
 * false - added -rc at the end jar
 */
ext.isRelease = false


jar {
    archiveName = generateJarName()
    from {
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'edu.nuos.detchrdevice.app.App'
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

private String generateJarName() {
    String name = "${ext.jarName}_v${ext.versionMajor}.${ext.versionMinor}"
    if (!ext.isRelease) {
        name += "-rc"
    }
    name += ".jar"
    return name;
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'net.sf.jchart2d:jchart2d:3.3.2'
    compile 'org.scream3r:jssc:2.8.0'
}

task(runApp, dependsOn: 'classes', type: JavaExec) {
    main = 'edu.nuos.detchrdevice.app.App'
    classpath = sourceSets.main.runtimeClasspath
}
